#!/usr/bin/perl

use v5.10;
use JSON::MaybeXS;
use ARGV::Struct;
use CloudHealth::API;
use Moo;
use Types::Standard qw/ArrayRef Bool Str Maybe/;

has argv => (is => 'ro', isa => ArrayRef, required => 1);
has ch => (is => 'ro', lazy => 1, default => sub { CloudHealth::API->new });

has run_method => (is => 'ro', isa => Maybe[Str], lazy => 1, default => sub { shift->argv->[0] });

has method_specified => (is => 'ro', isa => Bool, lazy => 1, default => sub {
  my $self = shift;
  defined $self->run_method;
});
has method_exists => (is => 'ro', isa => Bool, lazy => 1, default => sub {
  my $self = shift;
  return 0 if (not $self->method_specified);
  return 1 if ($self->ch->can($self->run_method));
});

sub usage {
  my $self = shift;
  say "Didn't specify the method to call" if (not $self->method_specified);
  say sprintf("Don't know method %s", $self->run_method) if (not $self->method_exists);
  if (not $self->method_specified or not $self->method_exists) {
    say 'The following methods are available:';
    foreach my $kind (keys %{ $self->ch->method_classification }) {
      foreach my $method (@{ $self->ch->method_classification->{ $kind } }) {
        say $method
      }
    }
    say '';
  }
  say "Usage: $0 method param1 param1value param2 param2value";
  return 1;
}

sub run {
  my ($self) = shift;

  return $self->usage if (not $self->method_specified);
  return $self->usage if (not $self->method_exists);
  
  my @params = @{ $self->argv };
  shift @params;
  my $arg_parser = ARGV::Struct->new(argv => [ '{', @params, '}' ]);
  my $struct = $arg_parser->parse;

  my $method = $self->run_method;
  my $result = eval {
    $self->ch->$method(%$struct);
  };
  if ($@) {
    say $@;
    return 1;
  }
  if (ref($result)) {
    my $json = JSON::MaybeXS->new->pretty;
    say $json->encode($result);
  }
  return 0;
}

exit __PACKAGE__->new(argv => [ @ARGV ])->run;
